import "StdLibrary.fcc" as stdLibrary
import "EditorGenLib.fcc" as editorGenLib
import Ball_Manager from "EditorGenLib.fcc"
import Player_Ability from "EditorGenLib.fcc"
import Player_Config from "EditorGenLib.fcc"
import Ball_Manager from "EditorGenLib.fcc"
import Resources from "EditorGenLib.fcc"

import "Physics.fcc" as physics 
import "LevelObject.fcc" as levelObject 
import "Combat.fcc" as combat 
import "Playable.fcc" as playable 
import "./VisualEffectUtils.fcg" as visualEffectUtils
import "./TextObjectUtils.fcg" as textObjectUtils
import "./MotionUtils.fcg" as motionUtils
import "Player.fcc" as player
import "Workflow.fcc" as workflow 
import "Hostility.fcc" as hostility 


graph Invisible_Ability {

    isUsingSkill bool;
    auraEffect EffectEntity;
    auraFollowMotion PositionFollow;
    textObject TextEntity;
    event OnFire() {
        SphereOverlap(thisEntity<Player>.hitRange,thisEntity<Player>.Position+Vector3{0, 0.5, 0},List<int>{1},false,out var listOfEntity);

        for key, ball in listOfEntity {
            DispatchEvent(OnPlayerHitTheBall,ball<Entity>,List<object>{thisEntity<Player>,nil});
        }
        
    }
    event OnRoundEnd(roundIndex int) {
        DeleteAllEffect();
    }

    event OnBeKilled(attacker entity<Entity>, damageType DamageType, beDamagedPart HitBodyPart) {
        LogInfo("Delete Effect");
        DeleteAllEffect();
    }

    event OnPlayerUseAbility() {
        start CastInvsibility();
    }

    event OnPhaseStart(phase entity<Phase>) {
        if (phase<Phase>.PhaseIndex==Resources:phaseIndexEnum["StageEnds"]){
            LogInfo("Delete this ability");
            DeleteAllEffect();
        }
    }

    async func CastInvsibility(){
        PlaySound(thisEntity<Player>,SoundEffects:Apparition);

        isUsingSkill = true;
        TurnOnEffect();
        thisEntity<Visibility>.Visible = false;
        if(textObject==nil){
            textObject = textObjectUtils:CreateText(Resources:skillNameLocalization["Invisible"],#FFFF0FFF,thisEntity<Player>.Position);
        }
        start motionUtils:FloatingTextMotion(textObject,1000,thisEntity<Player>.Position);        
        WaitForSeconds(10000);
        isUsingSkill = false;
        thisEntity<Visibility>.Visible = true;
        TurnOffEffect();
        thisEntity<Player_Ability>.StartCD();
    }

    func TurnOnEffect(){

        if(auraEffect==nil){
            auraEffect = visualEffectUtils:CreateVisualEffectSelf(EffectType:UpwardTurbulence,#F5F5F5FF,Vector3{1, 2, 1},Vector3{0, 0, 0},thisEntity<Player>) as EffectEntity;
            auraFollowMotion = visualEffectUtils:CraeateEffectEntityFollowPlayer(auraEffect<EffectEntity>,thisEntity<Entity>,Vector3{0, .8, 0});
        }else{
            SetActive(auraEffect<Entity>,true);
        }

    }
    func TurnOffEffect(){
       
        SetActive(auraEffect<Entity>,false);
    }

    async func DeleteAllEffect(){
        thisEntity<Visibility>.Visible = true;
        DeleteEntity(auraEffect<Entity>);
        DeleteEntity(auraFollowMotion<Entity>);
        DeleteEntity(textObject<Entity>);
        WaitForNextFrame();

        thisEntity<Player_Ability>.DeleteCurrentAbility();
    }

}